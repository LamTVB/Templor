Language templor;

Lexer

    lower = 'a'..'z';
    upper = 'A'..'Z';
    number = ('0'..'9')+;
    string = Shortest '"' string_char* '"';
    string_char = Any - (#10 | #13);


    id = lower name_char*;
    name_char = upper | lower | '_';
    template_def = Shortest '<{' Any* '}>';

    blank = ' ' | #13 | #10 | #9;
    comment = Shortest '/*' Any* '*/';

    Token
        template_def;

    Ignored
        blank, comment;

Parser

    program = stms;

    stms =
        {more:} stms stm |
        {zero:} ;

    stm =
        {print:} 'print' [lp:]'(' add_template ')' ';' |
        {render:} 'render' [lp:]'(' add_template ')' ';'|
        {create:} [template_name:]id [eq:]':=' add_template ';'|
        {call:} template [call:]'::' id ';' |
        {populate:} 'populate' [lp:]'(' id ',' string ')' ';';

    add_template =
        {add:} add_template [plus:]'+' template |
        {simple:} template;

    template =
        {template_def:} template_def |
        {template_name:} id;







